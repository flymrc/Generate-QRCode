// Top-level build file where you can add configuration options common to all sub-projects/modules.
buildscript {
    ext.kotlin_version = "1.4.21"
    ext.kotlin_coroutine_version = '1.4.1'

    repositories {
        maven { url 'https://maven.aliyun.com/repository/google/' }
        maven { url 'https://jitpack.io' }
        google()
        jcenter()
        mavenCentral()
        maven { url 'https://maven.aliyun.com/repository/public/' }
    }
    dependencies {
        classpath "com.android.tools.build:gradle:4.1.2"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"

        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
    }
}

plugins {
    id "com.diffplug.spotless" version "5.7.0"
}

spotless {
    java {
        target '*/src/**/*.java'
        googleJavaFormat()
    }

    kotlin {
        target '*/src/**/*.kt'
        ktlint('0.39.0').userData(['disabled_rules': 'no-wildcard-imports'])
    }

    format 'xml', {
        target 'app/src/main/**/*.xml'
        eclipseWtp('xml').configFile('spotless.xml.prefs')
    }

    format 'gradle', {
        target '**/*.gradle'
        indentWithSpaces()
        trimTrailingWhitespace()
        endWithNewline()
    }
}

task updatePrepush(type: Exec) {
    inputs.files 'devtools/pre-push.sh'
    outputs.files '.git/hooks/pre-push'

    commandLine 'cp', '-f', inputs.files[0], outputs.files[0]
}

gradle.startParameter.taskNames = [":updatePrepush"] + gradle.startParameter.taskNames

allprojects {
    repositories {
        google()
        jcenter()
    }
}
